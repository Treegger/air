<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark"
		xmlns:mx="library://ns.adobe.com/flex/mx"
		gap="2"
		width="100%"
		height="100%" 
		creationComplete="creationComplete()"
		show="if( initialized ) textInput.setFocus()"
		 >
	<fx:Script>
		<![CDATA[
			import com.treegger.airim.controller.ChatController;
			import com.treegger.airim.controller.Contact;
			import com.treegger.protobuf.TextMessage;
			
			import spark.components.Label;
			
			public var contact:Contact;
			
			[Inject]
			public var chatController:ChatController;
			
			private function creationComplete():void
			{
				displayPendingTextMessage();
				textInput.setFocus();
			}
			
			private var lastChatItem:ChatItem;
			
			
			private function toXML( s:String ):String
			{
				return s.replace( /&/g, "&amp;" ).replace( /</g, "&lt;" ).replace( />/g, "&gt;" ).replace( /\'/g, "&apos;" ).replace( /\"/g, "&quot;" );
			}
			private function fromXML( s:String ):String
			{
				return s.replace( /&lt;/g, "<"  ).replace( /&gt;/g, ">" ).replace( /&apos;/g, "'" ).replace( /&quot;/g, "\""  ).replace( /&amp;/g, "&"  );
			}
			
			private function pushText( fromSelf:Boolean, text:String ):void
			{
				var title:String = "You";
				if( !fromSelf ) title = contact.name;

				
				if( lastChatItem == null || lastChatItem.title != title )
				{
						lastChatItem = new ChatItem();
						lastChatItem.title = title;
						lastChatItem.titleColor = 0xffffff;
						lastChatItem.color = 0x000000;
						
						if( fromSelf )
						{
							lastChatItem.backgroundColor = 0xaa8a8a;
						}
						else
						{
							lastChatItem.backgroundColor = 0x8a8aaa;
						}
						
						lastChatItem.percentWidth = 100;
						areaInput.addElement( lastChatItem );
						
						lastChatItem.addEventListener(Event.ADDED, function(event:Event):void {  scrollingAnimation.play() } );
				}
				
				
				lastChatItem.addText( text );
				
			}
			
			private function enterHandler( event:Event ):void
			{
				if( textInput.text )
				{
					const text:String = textInput.text;
					pushText( true, text );
					chatController.sendTextMessage( contact.jid, toXML( text ) , null );
					
					textInput.text = "";
				}
			}
			public function displayPendingTextMessage():void
			{
				if( initialized )
				{
					for each( var textMessage:TextMessage in contact.textMessages )
					{
						if( textMessage.body ) pushText( false, textMessage.body );
					}
					contact.textMessages.removeAll();
				}
			}
			
			
			private function dragEnterHandler(e:NativeDragEvent):void 
			{
				if (e.clipboard.hasFormat(ClipboardFormats.TEXT_FORMAT)) {
					NativeDragManager.acceptDragDrop(textInput);
				}
			}
			
			private function dropHandler(e:NativeDragEvent):void 
			{
				trace( "Drop " + e );
				textInput.text += e.clipboard.getData(ClipboardFormats.TEXT_FORMAT) as String;
			}

		]]>
	</fx:Script>
	<fx:Declarations>
		<s:Animate  id="scrollingAnimation" target="{areaInput}" duration="200">
			<s:SimpleMotionPath property="verticalScrollPosition" valueTo="{areaInput.contentHeight - areaInput.height}"/>
		</s:Animate>
	</fx:Declarations>
	
	<s:Group  height="100%" width="100%">
		<s:Rect height="100%" width="100%">
			<s:fill>
				<s:SolidColor color="0xffffff" alpha="1" />
			</s:fill>
			<s:stroke>
				<s:SolidColorStroke color="0x000000" alpha=".3" />
			</s:stroke>
		</s:Rect>

		<s:Scroller id="scroller" height="100%" width="100%">
				<s:VGroup id="areaInput"  paddingTop="5" paddingBottom="5" paddingLeft="5" paddingRight="5">
					<mx:Spacer height="100%"/>
				</s:VGroup>
		</s:Scroller>
	</s:Group>
	
	
	<s:TextInput id="textInput" width="100%" enter="enterHandler( event )"
				 nativeDragEnter="dragEnterHandler(event)" 
				 nativeDragDrop="dropHandler(event)"
				 
				 />
</s:VGroup>
