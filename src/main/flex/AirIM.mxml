<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication 
   	xmlns:s="library://ns.adobe.com/flex/spark"
   	xmlns:fx="http://ns.adobe.com/mxml/2009"
	xmlns:swiz="http://swiz.swizframework.org"
	xmlns:view="com.treegger.airim.view.*"
	xmlns:local="*"
	
	creationComplete="creationCompleteHandler()" 
	windowActivate="windowActivateHandler()"
	backgroundAlpha="0"
	showStatusBar="false"
	useNativeDragManager="true"  
	autoExit="false"

>
	<fx:Metadata>
		[ResourceBundle("airim")]
	</fx:Metadata>
	
	<fx:Script>
	<![CDATA[
		import air.update.ApplicationUpdaterUI;
		import air.update.events.UpdateEvent;
		
		import com.treegger.airim.controller.ChatController;
		import com.treegger.airim.controller.PreferencesController;
		import com.treegger.airim.model.UserPreference;
		import com.treegger.airim.view.PreferencesWindow;
		import com.treegger.component.Notification;
		
		import mx.controls.Alert;
		
		import org.swizframework.core.SwizManager;
		
		import spark.components.Application;
		import spark.filters.DropShadowFilter;
		
		private var appUpdater:ApplicationUpdaterUI = new ApplicationUpdaterUI();
	
		private var preferencesSet:Boolean = false;
		
		private function windowActivateHandler():void
		{
			if( !preferencesSet )
			{
				preferencesSet = true;
				var preferencesController:PreferencesController = swizBean.preferencesController;
				if( !preferencesController.userAccount )
				{
					openPreferencesWindow();
				}
				
				if( preferencesController.hasUserPreference() )
				{
					const pref:UserPreference = preferencesController.userPreference;
					if( pref.applicationX > 0 ) nativeWindow.x =  pref.applicationX;
					if( pref.applicationY > 0 ) nativeWindow.y =  pref.applicationY;
				}
			}
			
		}
		
		
		private function creationCompleteHandler():void
		{

			this.addEventListener(MouseEvent.MOUSE_DOWN, onMouseDown);
			
			
			var shadowFilter:DropShadowFilter = new DropShadowFilter();
			shadowFilter.color = 0x000000;
			shadowFilter.alpha = 0.2;
			shadowFilter.blurX = 10;
			shadowFilter.blurY = 10;
			shadowFilter.distance = 5;
			filters = [shadowFilter];

			// Active / Inactive app 
			nativeApplication.addEventListener(Event.ACTIVATE, onApplicationActivate); 
			nativeApplication.addEventListener(Event.DEACTIVATE, onApplicationDeactivate);
			nativeApplication.addEventListener(Event.EXITING, quitApplicationHandler );
			
			addEventListener(Event.CLOSING, function( event:Event ):void
			{
				if( !nativeWindow.closed && nativeWindow.active )
				{
					var pref:UserPreference = swizBean.preferencesController.userPreference;
					pref.applicationX = nativeWindow.x;
					pref.applicationY = nativeWindow.y;
					
					swizBean.preferencesController.userPreference = pref;
				}	

				if( !exitingApplication )
				{
					nativeWindow.visible=false;
					event.preventDefault()
				}
			}); 
			
			
			nativeApplication.autoExit = false;
			if( NativeApplication.supportsDockIcon )
			{
				nativeApplication.addEventListener( InvokeEvent.INVOKE, onShowWindow );				
			}
			else if( NativeApplication.supportsSystemTrayIcon )
			{
				SystemTrayIcon( NativeApplication.nativeApplication.icon ).addEventListener( ScreenMouseEvent.CLICK, onShowWindow );
			}
			
			
			
			//NativeApplication.nativeApplication.idleThreshold = 2 * 60; // In seconds -- the default is 5 minutes.
			NativeApplication.nativeApplication.addEventListener(Event.USER_IDLE, onUserIdle);
			NativeApplication.nativeApplication.addEventListener(Event.USER_PRESENT, onUserPresent); 
			
			
			createMenu();
			
			
			checkForUpdate();
		}
		
		
		private function createMenu():void
		{
			var airIMmenu:NativeMenu = new NativeMenu();
			
			const preferenceMenuItem:NativeMenuItem = new NativeMenuItem( resourceManager.getString('airim', 'preferences' ) );
			preferenceMenuItem.addEventListener(Event.SELECT, preferencesSelectedHandler );
			airIMmenu.addItem( preferenceMenuItem ) ;
			
			
			
			
			airIMmenu.addItem(new NativeMenuItem( "", true ) );
			const quitMenuItem:NativeMenuItem = airIMmenu.addItem(new NativeMenuItem( resourceManager.getString('airim', 'quitAirIM' ) ));
			quitMenuItem.keyEquivalent = "q";
			quitMenuItem.addEventListener(Event.SELECT, quitApplicationHandler );
			contextMenu = airIMmenu;
			
			if( NativeApplication.supportsMenu )
			{
				const mainMenu:NativeMenuItem = NativeApplication.nativeApplication.menu.getItemAt(0);
				const preferenceMenuItem2:NativeMenuItem = new NativeMenuItem( resourceManager.getString('airim', 'preferences' ) );
				preferenceMenuItem2.addEventListener(Event.SELECT, preferencesSelectedHandler );
				mainMenu.submenu.addItemAt( preferenceMenuItem2, 2 );
				mainMenu.submenu.items[mainMenu.submenu.items.length-1].addEventListener( Event.SELECT, teardown )
				// = mainMenu;
			}
			
		}
		
		private function preferencesSelectedHandler( event:Event ):void
		{
			openPreferencesWindow();
		}

		private function openPreferencesWindow():void
		{
			const window:PreferencesWindow = new PreferencesWindow() ;
			SwizManager.setUp(window);
			window.open(true);
		}
		
		private var exitingApplication:Boolean;
		private function teardown( event:Event = null ):void
		{
			trace( "Teardown" );
			if( !exitingApplication )
			{
				exitingApplication = true;
				swizBean.chatController.sendPresence( "unavailable" );
				swizBean.chatController.close();
			}
		}
		
		private function quitApplicationHandler( event:Event = null ):void
		{
			trace( "Quit" );

			for each( var win:NativeWindow in NativeApplication.nativeApplication.openedWindows ) 
			{
				win.dispatchEvent( new Event( Event.CLOSING, false, true ) );
				win.close();
			}
			teardown();
			exit();
		}
		
		
		private function onUserIdle(e:Event):void 
		{ 
			// No keyboard or mouse input for n minutes. 
			swizBean.chatController.sendPresence( null, "away" );

		}
		private function onUserPresent(e:Event):void 
		{
			// The user is back!
			swizBean.chatController.sendPresence();
		}
		
		private function onShowWindow(e:Event):void 
		{
			if( !nativeWindow.closed )
			{
				nativeWindow.visible=true;
				nativeWindow.activate();
			}
			
		}
		
		private function onApplicationActivate(e:Event):void 
		{
			stage.frameRate = 24;
		}  
		private function onApplicationDeactivate(e:Event):void 
		{
			stage.frameRate = 10; 
		}
		
		private function onMouseDown(evt:MouseEvent):void
		{
			if( !nativeWindow.closed ) stage.nativeWindow.startMove();
		}
		
		
		private function checkForUpdate():void {
			setApplicationVersion(); 			
			appUpdater.updateURL = "http://ws.treegger.com/air/update.xml"; 
			appUpdater.isCheckForUpdateVisible = false; // We won't ask permission to check for an update
			appUpdater.addEventListener(UpdateEvent.INITIALIZED, onUpdate); 
			appUpdater.addEventListener(ErrorEvent.ERROR, onError); 
			appUpdater.initialize();
		}
	
		private function onError(event:ErrorEvent):void {
			trace( "CheckUpdate error: " + event );
		}
		
		private function onUpdate(event:UpdateEvent):void {
			appUpdater.checkNow();
		}
	
		// Find the current version for our Label below
		private function setApplicationVersion():void {
			var appXML:XML = NativeApplication.nativeApplication.applicationDescriptor;
			var ns:Namespace = appXML.namespace();
			trace( "Current version is " + appXML.ns::version );
		}
		
	]]>
	</fx:Script>
	
	<fx:Style source="css/AirIM.css" />
	
	<fx:Declarations>
		<swiz:Swiz>
			<swiz:beanProviders>
				<local:Beans id="swizBean"/>
			</swiz:beanProviders>
			
			<swiz:config>
				<swiz:SwizConfig
					setUpEventType="{ Event.ADDED_TO_STAGE }" 
					setUpEventPhase="{ EventPhase.CAPTURING_PHASE }" 
					setUpEventPriority="50"
					tearDownEventType="{ Event.REMOVED_FROM_STAGE }" 
					tearDownEventPhase="{ EventPhase.CAPTURING_PHASE }" 
					tearDownEventPriority="50"

					viewPackages="com.treegger.airim.view"
					
					defaultDispatcher="global" />
			</swiz:config>
		</swiz:Swiz>
	</fx:Declarations>

	
	<view:RosterView id="rosterView" resize="height = rosterView.height+10; width = rosterView.width+10;" />
	
</s:WindowedApplication>
