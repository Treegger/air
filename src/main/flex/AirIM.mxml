<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication 
   	xmlns:s="library://ns.adobe.com/flex/spark"
   	xmlns:fx="http://ns.adobe.com/mxml/2009"
	creationComplete="creationCompleteHandler()" 
	backgroundAlpha="0"
	showStatusBar="false"
	useNativeDragManager="true"
	
	
>
	<fx:Script>
	<![CDATA[
		import air.update.ApplicationUpdaterUI;
		import air.update.events.UpdateEvent;
		
		import com.treegger.service.TreeggerService;
		
		import flash.events.ErrorEvent;
		import flash.text.engine.ElementFormat;
		import flash.text.engine.FontDescription;
		import flash.text.engine.TextBlock;
		import flash.text.engine.TextElement;
		import flash.text.engine.TextLine;
		
		import mx.collections.ArrayCollection;
		import mx.controls.Alert;
		
		import spark.filters.BevelFilter;
		import spark.filters.DropShadowFilter;
		
		
		[Embed(source="icons/logo-128x128.png")]
		[Bindable]
		private var ApplicationLogo:Class;     
		
		private var appUpdater:ApplicationUpdaterUI = new ApplicationUpdaterUI();
	
		private const treeggerService:TreeggerService = new TreeggerService();
		
		private function creationCompleteHandler():void
		{
			this.addEventListener(MouseEvent.MOUSE_DOWN, onMouseDown);
			var shadowFilter:DropShadowFilter = new DropShadowFilter();
			shadowFilter.color = 0x000000;
			shadowFilter.alpha = 0.4;
			shadowFilter.blurX = 5;
			shadowFilter.blurY = 5;
			shadowFilter.distance = 5;
			filters = [shadowFilter];

			this.height = box.height+4;
			this.width = box.width+4;
			
			
			
			// Active / Inactive app 
			nativeApplication.addEventListener(Event.ACTIVATE, onApplicationActivate); 
			nativeApplication.addEventListener(Event.DEACTIVATE, onApplicationDeactivate);
			
			
			nativeApplication.autoExit = false;
			if( NativeApplication.supportsDockIcon )
			{
				nativeApplication.addEventListener( InvokeEvent.INVOKE, onShowWindow );				
			}
			else if( NativeApplication.supportsSystemTrayIcon )
			{
				SystemTrayIcon( NativeApplication.nativeApplication.icon ).addEventListener( ScreenMouseEvent.CLICK, onShowWindow );
			}
			
			
			//NativeApplication.nativeApplication.idleThreshold = 2 * 60; // In seconds -- the default is 5 minutes.
			NativeApplication.nativeApplication.addEventListener(Event.USER_IDLE, onUserIdle);
			NativeApplication.nativeApplication.addEventListener(Event.USER_PRESENT, onUserPresent); 
		
			createMenu();
			
			
			//checkForUpdate();
		}

		private function createMenu():void
		{
			var airIMmenu:NativeMenu = new NativeMenu();
			
			const preferenceMenuItem:NativeMenuItem = new NativeMenuItem("Preferences...");
			airIMmenu.addItem( preferenceMenuItem ) ;
			airIMmenu.addItem(new NativeMenuItem( "", true ) );
			const quitMenuItem:NativeMenuItem = airIMmenu.addItem(new NativeMenuItem("Quit AirIM"));
			quitMenuItem.keyEquivalent = "q";
			quitMenuItem.addEventListener(Event.SELECT, quitApplicationHandler );
			contextMenu = airIMmenu;
			
			if( NativeApplication.supportsMenu )
			{
				const mainMenu:NativeMenuItem = NativeApplication.nativeApplication.menu.getItemAt(0);
				mainMenu.submenu.addItemAt(new NativeMenuItem("Preferences..."), 2);
				mainMenu.submenu.items[mainMenu.submenu.items.length-1].addEventListener( Event.SELECT, quitApplicationHandler )
				// = mainMenu;
			}
			
		}
		
		private function quitApplicationHandler( event:Event = null ):void
		{
			trace( "Quit" );
			NativeApplication.nativeApplication.exit(0);	
		}
		
		private function notifyDockIcon():void
		{
			if( NativeApplication.supportsDockIcon )
			{
				
				var unseenCount:uint = 10// Function for counting the number of unread messages. 
				var unreadCountSprite:Sprite = new Sprite(); 
				unreadCountSprite.width = 128; 
				unreadCountSprite.height = 128; 
				unreadCountSprite.x = 0; 
				unreadCountSprite.y = 0; 
				var padding:uint = 16; 
				// Use FTE APIs to get the best looking text. 
				var fontDesc:FontDescription = new FontDescription("Arial", "bold"); 
				var elementFormat:ElementFormat = new ElementFormat(fontDesc, 26, 0xFFFFFF); 
				var textElement:TextElement = new TextElement(String(unseenCount), elementFormat); 
				var textBlock:TextBlock = new TextBlock(textElement); 
				var textLine:TextLine = textBlock.createTextLine(); 
				
				var diameter:uint = padding;
				if( textLine.textWidth < textLine.textHeight ) diameter += textLine.textHeight;  
				else diameter += textLine.textWidth;
				
				textLine.x = 128 - diameter/2 - textLine.textWidth/2 - 4; 
				textLine.y = diameter/2 + textLine.textHeight/2 - 3;
				
				
				unreadCountSprite.graphics.beginFill(0xE92200); 
				unreadCountSprite.graphics.drawEllipse( 128 - diameter - 4, 0, diameter, diameter); 
				unreadCountSprite.graphics.endFill(); 
				unreadCountSprite.addChild(textLine); 
				var shadow:DropShadowFilter = new DropShadowFilter(3, 45, 0, .75); 
				var bevel:BevelFilter = new BevelFilter(1); 
				unreadCountSprite.filters = [shadow.clone(),bevel.clone()];
				
				var unreadCountData:BitmapData = new BitmapData(128, 128, true, 0x00000000);  
				// The Dynamic128IconClass referenced below is embedded. 
				var appData:BitmapData = (new ApplicationLogo).bitmapData;  
				unreadCountData.draw( appData );
				unreadCountData.draw( unreadCountSprite );
				var appIcon:Bitmap = new Bitmap(unreadCountData); 
				// If you do want to change the system tray icon on Windows, as well, add a 16x16 icon to the array below. 
				InteractiveIcon(NativeApplication.nativeApplication.icon).bitmaps = [appIcon];
				
				var dock:DockIcon = NativeApplication.nativeApplication.icon as DockIcon;
				dock.bounce(NotificationType.INFORMATIONAL);
				
				var screen:Screen = Screen.mainScreen;
				
				var notif:Notification = new Notification("None of your friend are online");
				notif.bounds = new Rectangle(screen.visibleBounds.width - notif.width - 2, screen.visibleBounds.y + 2, notif.width, notif.height );
				notif.alwaysInFront = true;
				notif.visible = true;
				
			}

		}
		
		private function onUserIdle(e:Event):void 
		{ 
			// No keyboard or mouse input for 2 minutes. 
		}
		private function onUserPresent(e:Event):void 
		{
			// The user is back! 
		}
		
		private function onShowWindow(e:Event):void 
		{
			nativeWindow.activate(); 
		}
		
		private function onApplicationActivate(e:Event):void 
		{
			stage.frameRate = 24; 
		}  
		private function onApplicationDeactivate(e:Event):void 
		{
			stage.frameRate = 10; 
		}
		
		private function onMouseDown(evt:MouseEvent):void
		{
			stage.nativeWindow.startMove();
		}
		
		
		private function checkForUpdate():void {
			setApplicationVersion(); // Find the current version so we can show it below			
			appUpdater.updateURL = "http://ws.treegger.com/air/update.xml"; // Server-side XML file describing update
			appUpdater.isCheckForUpdateVisible = false; // We won't ask permission to check for an update
			appUpdater.addEventListener(UpdateEvent.INITIALIZED, onUpdate); // Once initialized, run onUpdate
			appUpdater.addEventListener(ErrorEvent.ERROR, onError); // If something goes wrong, run onError
			appUpdater.initialize(); // Initialize the update framework
		}
	
		private function onError(event:ErrorEvent):void {
			Alert.show(event.toString());
		}
		
		private function onUpdate(event:UpdateEvent):void {
			appUpdater.checkNow(); // Go check for an update now
		}
	
		// Find the current version for our Label below
		private function setApplicationVersion():void {
			var appXML:XML = NativeApplication.nativeApplication.applicationDescriptor;
			var ns:Namespace = appXML.namespace();
			ver.text = "Current version is " + appXML.ns::version;
		}
		
	]]>
	</fx:Script>
	
		<s:VGroup gap="1" id="box">
			<s:Label id="ver" />
			<s:Button label="Bounce" click="notifyDockIcon()"/>
			<s:Button label="Connect" click="treeggerService.connect()"/>
			<s:HGroup>
				<s:TextInput id="user" />
				<s:ComboBox id="socialNetwork" dataProvider="{new ArrayCollection(['Twitter','Foursquare'])}" selectedIndex="0"/>
				<s:TextInput id="password" displayAsPassword="true"/>
				
				<s:Button label="Auth" click="treeggerService.authenticate( user.text, socialNetwork.selectedItem.toString().toLocaleLowerCase(), password.text, 'AirIM' )"/>
				
			</s:HGroup>
			
			
			<s:List id="rosterList" borderVisible="false" contentBackgroundAlpha="0"
					rollOver="event.preventDefault(); event.stopPropagation()" itemRenderer="RosterItem">
				<s:dataProvider>
					<s:ArrayList>
						<fx:Object firstName="Ann"  lastName="Green" />
						<fx:Object firstName="Tom"  lastName="Smith" />
						<fx:Object firstName="John" lastName="Black" />
						<fx:Object firstName="Jane" lastName="White" />
						<fx:Object firstName="Bill" lastName="Jones" />
					</s:ArrayList>
				</s:dataProvider>
			</s:List>
		</s:VGroup>
	
</s:WindowedApplication>
